import org.ajoberstar.grgit.Grgit

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "$repoUrl" }
    jcenter()
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
  }
}

apply plugin: 'com.google.protobuf'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

springBoot {
  mainClassName = 'com.sequenceiq.caas.MockCaasApplication'
}

jar {
  archiveBaseName = 'mock-caas'
  archiveFileName = 'mock-caas.jar'
}

bootJar {
  archiveBaseName = 'mock-caas'
  archiveFileName = 'mock-caas.jar'
  manifest {
    def grgit = Grgit.open(currentDir: project.projectDir)
    attributes "Implementation-Version": grgit.describe()
  }
}

dependencies {
//  compile project(':core-api')
  compile ("io.grpc:grpc-all:1.19.0") {
    exclude group: 'org.mockito'
  }
  compile group:  'org.springframework.boot',     name: 'spring-boot-starter',            version: springBootVersion
  compile group:  'org.springframework.boot',     name: 'spring-boot-starter-security',   version: springBootVersion
  compile group:  'org.springframework.boot',     name: 'spring-boot-starter-jersey',     version: springBootVersion
  compile group:  'org.springframework.boot',     name: 'spring-boot-starter-web',        version: springBootVersion
  compile group:  'org.springframework.boot',     name: 'spring-boot-starter-actuator',   version: springBootVersion
  compile group:  'org.springframework',          name: 'spring-context-support',         version: springFrameworkVersion
  compile group:  'org.springframework.security', name: 'spring-security-jwt',            version: '1.0.10.RELEASE'
  compile group:  'com.fasterxml.jackson.core',   name: 'jackson-core',                   version: '2.9.7'
  compile group:  'io.jsonwebtoken',              name: 'jjwt',                           version: '0.9.1'
  testCompile group: 'org.mockito',               name: 'mockito-core',                   version: mockitoVersion
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

protobuf {
  protoc {
    // The version of protoc must match protobuf-java. If you don't depend on
    // protobuf-java directly, you will be transitively depending on the
    // protobuf-java version that grpc depends on.
    artifact = "com.google.protobuf:protoc:3.6.1"
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.12.0'
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
  generatedFilesBaseDir = "$projectDir/src/generated"
}